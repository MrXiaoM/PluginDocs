---
title: 构建脚本
date: 2025-06-04 19:41:58
permalink: /elopers/base/buildscript
---

[<< 返回开发文档](/elopers/base/intro)

## 构建脚本

依赖于 Gradle

首先是 wrapper 配置 `gradle/wrapper/gradle-wrapper.properties`  
推荐使用 8.5 版本，所支持 Java 21 的最低 Gradle 版本
```properties
distributionBase=GRADLE_USER_HOME
distributionPath=wrapper/dists
distributionUrl=https\://services.gradle.org/distributions/gradle-8.5-bin.zip
zipStoreBase=GRADLE_USER_HOME
zipStorePath=wrapper/dists
```

编写构建脚本 build.gradle.kts 如下
```kotlin
plugins {
    java
    `maven-publish`
    id ("com.gradleup.shadow") version "8.3.0"
}

group = "top.mrxiaom"
version = "1.0.0-SNAPSHOT"

var shadowGroup = "top.mrxiaom.yourplugin.utils"
val targetJavaVersion = 8

repositories {
    mavenCentral()
    maven("https://repo.papermc.io/repository/maven-public/") 
    maven("https://repo.codemc.io/repository/maven-public/")
    maven("https://hub.spigotmc.org/nexus/content/repositories/snapshots/")
    maven("https://jitpack.io")
    maven("https://repo.rosewooddev.io/repository/public/") // NMS
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:1.20-R0.1-SNAPSHOT")
    // NMS: https://repo.rosewooddev.io/service/rest/repository/browse/public/org/spigotmc/spigot/
    compileOnly("org.spigotmc:spigot:1.20")
    compileOnly("org.jetbrains:annotations:24.0.0")

    implementation("top.mrxiaom.pluginbase:library:1.6.0") // 导入 PluginBase 库
    // implementation("top.mrxiaom.pluginbase:paper:1.6.0") // 导入 Paper 交互库 (可选)
    // implementation("com.zaxxer:HikariCP:4.0.3") // 数据库连接池
}
java {
    val javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion.set(JavaLanguageVersion.of(targetJavaVersion))
    }
}
tasks {
    shadowJar {
        mapOf(
            // 一定要将 PluginBase 依赖给 relocate 到你的插件包中
            "top.mrxiaom.pluginbase" to "base",
            // "com.zaxxer.hikari" to "hikari",
        ).forEach { (original, target) ->
            relocate(original, "$shadowGroup.$target")
        }
    }
    val copyTask = create<Copy>("copyBuildArtifact") {
        dependsOn(shadowJar)
        from(shadowJar.get().outputs)
        rename { "${project.name}-$version.jar" }
        into(rootProject.file("out"))
    }
    build {
        dependsOn(copyTask)
    }
    withType<JavaCompile>().configureEach {
        options.encoding = "UTF-8"
        if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible) {
            options.release.set(targetJavaVersion)
        }
    }
    processResources {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
        from(sourceSets.main.get().resources.srcDirs) {
            expand(mapOf("version" to version))
            include("plugin.yml")
        }
    }
}
publishing {
    publications {
        create<MavenPublication>("maven") {
            from(components.getByName("java"))
            groupId = project.group.toString()
            artifactId = rootProject.name
            version = project.version.toString()
        }
    }
}
```
